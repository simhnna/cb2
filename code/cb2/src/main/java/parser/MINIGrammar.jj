/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */
options
{
  static = false;
}

PARSER_BEGIN(MINIGrammar)
package parser;
import java.io.FileInputStream;
import java.io.File;
import java.io.FileNotFoundException;
import components.*;
import components.interfaces.*;
import java.util.ArrayList;
import testsuite.MINIException;
import errorHandling.ErrorHandler;

@SuppressWarnings("unused")
public class MINIGrammar
{
  public static ArrayList < ClassNode > parse(File in) throws MINIException
  {
    try
    {
      MINIGrammar parser = new MINIGrammar(new FileInputStream(in));
      return parser.file();
    }
    catch (FileNotFoundException e)
    {
      e.printStackTrace();
      return null;
    }
    catch (ParseException e)
    {
      ErrorHandler.handleParseError(in, e.currentToken, e.tokenImage, e.expectedTokenSequences);
      return null;
    }
    catch (TokenMgrError e) {
		ErrorHandler.handleTokenMgrError(in, e);
		return null;
    }
  }
}

PARSER_END(MINIGrammar)

SKIP :
{
  " "
| "\r"
| "\t"
| "\n"
}

/* COMMENTS */

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
}

< IN_SINGLE_LINE_COMMENT >
SPECIAL_TOKEN :
{
    < SINGLE_LINE_COMMENT :
    "\n"
  | "\r"
  | "\r\n" > : DEFAULT
}

< IN_MULTI_LINE_COMMENT >
SPECIAL_TOKEN :
{
  < MULTI_LINE_COMMENT : "*/" > : DEFAULT
}

< IN_SINGLE_LINE_COMMENT, IN_MULTI_LINE_COMMENT >
MORE :
{
  < ~[ ] >
}

< DEFAULT >
TOKEN : /* Reserved keywords and such */
{
  < CLASS : "class" >
| < BRACE_OPEN : "{" >
| < BRACE_CLOSE : "}" >
| < PARAN_OPEN : "(" >
| < PARAN_CLOSE : ")" >
| < ARRAY_DEF : "[]" >
| < THIS : "this" >
| < DOT : "." >
| < COMMA : "," >
| < SEMICOLON : ";" >
| < NULL : "null" >
| < NEW : "new" >
| < RETURN : "return" >
| < VAR : "var" >
| < IF : "if" >
| < ELSE : "else" >
| < WHILE : "while" >
| < ASSIGNMENT : ":=" >
| < MINUS : "-" >
| < NEGATION : "!" >
| < PLUS : "+" >
| < MULTIPLY : "*" >
| < DIVIDE : "/" >
| < REMAINDER : "%" >
| < EQUAL : "==" >
| < NOTEQUAL : "!=" >
| < LESS_THAN_EQUAL : "<=" >
| < GREATER_THAN_EQUAL : ">=" >
| < LESS_THAN : "<" >
| < GREATER_THAN : ">" >
| < AND : "&&" >
| < OR : "||" >
| < BOOL :
    (
      "true"
    | "false"
    ) >
| < ID : ([ "a"-"z", "A"-"Z" ])+ >
| < INT : ([ "0"-"9" ]) { 1,8 } >
| < STRING :
    "\""
    (
      " "
    | "!"
    | [ "#"-"~" ]
    )*
    "\"" >
| < BAD_TOKEN : ~[ ] >
}

ArrayList < ClassNode > file() :
{
  ArrayList < ClassNode > classes = new ArrayList < ClassNode > ();
  ClassNode cls;
}
{
  (
    cls = mini_class()
    {
      classes.add(cls);
    }
  )*
  < EOF >
  {
    return classes;
  }
}

ClassNode mini_class() :
{
  ClassNode cls;
  MemberNode classMember;
}
{
  < CLASS >
  < ID > { cls = new ClassNode(token); }
  < BRACE_OPEN >
  (
    classMember = classMember()
    {
      cls.children.add(classMember);
    }
  )*
  < BRACE_CLOSE >
  {
    return cls;
  }
}

MemberNode classMember() :
{
  MethodNode m;
  Token memberName;
  Type type;
}
{
  type = type() memberName = < ID >
  (
    < SEMICOLON >
    {
      return new FieldNode(memberName, type);
    }
  | m = method(memberName, type)
  {
      return m;
    }
  )
}


MethodNode method(Token name, Type type) :
{
  BlockNode body;
  ArrayList<NamedType> arguments;
}
{
  arguments = signature() body = blockStatement()
  {
    return new MethodNode(name, type, arguments, body);
  }
}

Type type() :
{
  Token type;
  int dimensions = 0;
}
{
  type = < ID > (< ARRAY_DEF > { dimensions++; })*
  {
    return new Type(type, dimensions);
  }
}

ArrayList < NamedType > signature() :
{
  ArrayList < NamedType > arguments = new ArrayList < NamedType > ();
  Type type;
}
{
  < PARAN_OPEN >
  (
    type = type()
    < ID >
	{
      arguments.add(new NamedType(token, type));
    }
    (
      < COMMA >
      type = type()
      < ID >
      {
        arguments.add(new NamedType(token, type));
      }
    )*
 
  )?
  < PARAN_CLOSE >
  {
    return arguments;
  }
}

StatementNode statement() :
{
  StatementNode s = null;
}
{
  (
    s = blockStatement()
  | s = ifStatement()
  | s = whileStatement()
  | s = returnStatement()
  | s = simpleStatement()
  | s = declaration()
  )
  {
    return s;
  }
}

StatementNode simpleStatement() :
{
  ExpressionNode first, second;
}
{
  first = expression7()
  (
    < SEMICOLON > { return new SimpleStatementNode(first); }
    | < ASSIGNMENT > second = expression7() < SEMICOLON > {  return new AssignmentStatementNode(first, second); }
  )
}

DeclarationStatementNode declaration() :
{
  Token name;
  ExpressionNode expression;
}
{
  < VAR >
  < ID > { name = token; }
  < ASSIGNMENT >
  expression = expression7()
  < SEMICOLON >
  {
    return new DeclarationStatementNode(name, expression);
  }
}

BlockNode blockStatement() :
{
  BlockNode block = new BlockNode();
  StatementNode statement;
}
{
  < BRACE_OPEN >
  (
    statement=statement()
    {
      block.children.add(statement);
    }
  )*
  < BRACE_CLOSE >
  {
    return block;
  }
}

IfNode ifStatement() :
{
  ExpressionNode condition;
  BlockNode first, second = null;
}
{
  < IF > < PARAN_OPEN > condition = expression7()
  < PARAN_CLOSE > first = blockStatement()
  (
    < ELSE > second = blockStatement()
  )?
  {
    return new IfNode(condition, first, second);
  }
}

WhileNode whileStatement() :
{
  ExpressionNode condition;
  BlockNode body;
}
{
  < WHILE > < PARAN_OPEN > condition = expression7()
  < PARAN_CLOSE > body = blockStatement()
  {
    return new WhileNode(condition, body);
  }
}

ReturnNode returnStatement() :
{
  ExpressionNode returnValue;
}
{
  < RETURN > returnValue = expression7() < SEMICOLON >
  {
    return new ReturnNode(returnValue);
  }
}

ExpressionNode expression7 () :
//logical or
{
  ExpressionNode current;
  ExpressionNode next;
  Token operator;
}
{
  current=expression6() (operator=< OR >next = expression6() { current = new OrBinaryExpressionNode(operator, current, next); })*
  {
    return current;
  }
}

ExpressionNode expression6 () :
//logical and
{
  ExpressionNode current;
  ExpressionNode next;
  Token operator;
}
{
  current= expression5()(operator=< AND >next = expression5() { current = new AndBinaryExpressionNode(operator, current, next); })*
  {
    return current;
  }
}

ExpressionNode expression5 () :
//equality
{
  ExpressionNode current;
  ExpressionNode next;
  Token operator;
}
{
  current=expression4()(
    (operator=< EQUAL > next = expression4() { current= new EqBinaryExpressionNode(operator, current, next); })
    | (operator=< NOTEQUAL > next = expression4() { current= new NeqBinaryExpressionNode(operator, current, next); })
  )*
  {
    return current;
  }
}

ExpressionNode expression4 () :
//relational
{
  ExpressionNode current;
  ExpressionNode next;
  Token operator;
}
{
  current=expression3()(
    (operator=< LESS_THAN_EQUAL > next=expression3() { current = new LteBinaryExpressionNode(operator, current, next); })
    | (operator=< GREATER_THAN_EQUAL > next=expression3() { current = new GteBinaryExpressionNode(operator, current, next); })
    | (operator=< LESS_THAN > next=expression3() { current = new LtBinaryExpressionNode(operator, current, next); })
    | (operator=< GREATER_THAN > next=expression3() { current = new GtBinaryExpressionNode(operator, current, next); })
  )*
  {
    return current;
  }
}

ExpressionNode expression3 () :
//addition
{
  ExpressionNode current;
  ExpressionNode next;
  Token operator;
}
{
  current=expression2()(
    (operator=< PLUS > next=expression2() { current = new PlusBinaryExpressionNode(operator, current, next); })
    | (operator=< MINUS > next=expression2() { current = new MinusBinaryExpressionNode(operator, current, next); })
  )*
  {
    return current;
  }
}

ExpressionNode expression2 () :
//multiplicative
{
  ExpressionNode current;
  ExpressionNode next;
  Token operator;
}
{
  current=expression1()(
    (operator=< MULTIPLY > next=expression1() { current = new MultiplyBinaryExpressionNode(operator, current, next); })
    | (operator=< DIVIDE > next=expression1() { current = new DivideBinaryExpressionNode(operator, current, next); })
    | (operator=< REMAINDER > next=expression1() { current = new RemainderBinaryExpressionNode(operator, current, next); })
  )*
  {
    return current;
  }
}

ExpressionNode expression1 () :
//unary
{
  ExpressionNode current;
  Token operator;
}
{
  (    
    (
      (operator=< NEGATION > current = expression1() { current = new NegationUnaryExpressionNode(operator,current); })
      | (operator=< MINUS > current = expression1() { current = new MinusUnaryExpressionNode(operator, current); })
    )
    | current = expression0()
  )
  {
    return current;
  }
}

ExpressionNode expression0 () :
//atomic (+ member)
{
  ExpressionNode base = null;
  ExpressionNode current;
  ArrayList<ExpressionNode> arguments = null;
  Token name;
}
{
  current = atomicExpression()
  (
    < DOT > name=< ID >(arguments=argumentList())?
    {
	  if (arguments != null) {
		current = new MethodInvocationExpressionNode(current, name, arguments);
		arguments = null;
	  } else {
		current = new FieldMemberExpressionNode(current, name);
	  }
    }
  )*
  {
    return current;
  }
}

ArrayList<ExpressionNode> argumentList() :
{
  ArrayList<ExpressionNode> arguments = new ArrayList<ExpressionNode>();
  ExpressionNode expr;
}
{
  < PARAN_OPEN >
  (
    expr = expression7()
    {
      arguments.add(expr);
    }
    (
      < COMMA > expr = expression7()
      {
     	arguments.add(expr);
      }
    )*
  )?
  < PARAN_CLOSE >
  {
    return arguments;
  }
}

ExpressionNode atomicExpression() :
{
  ExpressionNode n;
  ExpressionNode expression;
  ArrayList<ExpressionNode> arguments = null;
  Token name;
}
{
  (
    name=< ID >
    ( arguments = argumentList())?
    {
      // baseObject is null because it doesn't exist here
      if(arguments == null){
        return new FieldMemberExpressionNode(null, name);
	  }
	  return new MethodInvocationExpressionNode(null, name, arguments);
    }
  | < THIS > { return new ThisAtomicExpressionNode(token); }
  | < STRING > { return new StringAtomicExpressionNode(token); }
  | < INT > { return new IntAtomicExpressionNode(token); }
  | < BOOL > { return new BoolAtomicExpressionNode(token); }
  | n = nullExpression()
  | n = newExpression()
  | < PARAN_OPEN >
    expression = expression7()
    < PARAN_CLOSE > { return new PriorityExpressionNode(expression); }
  )
{
  return n;
}
}

NullExpressionNode nullExpression() :
{
  Type type;
}
{
  < NULL > < LESS_THAN > type = type() < GREATER_THAN >
  {
    return new NullExpressionNode(type);
  }
}

NewExpressionNode newExpression() :
{
  NewExpressionNode n;
  Type type;
}
{
  < NEW > < LESS_THAN > type = type() { n = new NewExpressionNode(type); }
  (
    < COMMA >
    (
      < INT >
    | < ID >
    )
    {
      n.arguments.add(token);
    }
  )*
  < GREATER_THAN >
  {
    return n;
  }
}

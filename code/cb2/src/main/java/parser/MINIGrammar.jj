/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */
options
{
  static = false;
}

PARSER_BEGIN(MINIGrammar)
package parser;
import java.io.FileInputStream;
import java.io.File;
import java.io.FileNotFoundException;
import components.*;
import components.interfaces.*;
import java.util.ArrayList;
import testsuite.MINIException;
import errorHandling.ErrorHandler;

@SuppressWarnings("unused")
public class MINIGrammar
{
  public static ArrayList < ClassNode > parse(File in) throws MINIException
  {
    try
    {
      MINIGrammar parser = new MINIGrammar(new FileInputStream(in));
      return parser.file();
    }
    catch (FileNotFoundException e)
    {
      e.printStackTrace();
      return null;
    }
    catch (ParseException e)
    {
      ErrorHandler.handleParseError(in, e.currentToken, e.tokenImage, e.expectedTokenSequences);
      return null;
    }
    catch (TokenMgrError e) {
		ErrorHandler.handleTokenMgrError(in, e);
		return null;
    }
  }
}

PARSER_END(MINIGrammar)

SKIP :
{
  " "
| "\r"
| "\t"
| "\n"
}

/* COMMENTS */

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
}

< IN_SINGLE_LINE_COMMENT >
SPECIAL_TOKEN :
{
    < SINGLE_LINE_COMMENT :
    "\n"
  | "\r"
  | "\r\n" > : DEFAULT
}

< IN_MULTI_LINE_COMMENT >
SPECIAL_TOKEN :
{
  < MULTI_LINE_COMMENT : "*/" > : DEFAULT
}

< IN_SINGLE_LINE_COMMENT, IN_MULTI_LINE_COMMENT >
MORE :
{
  < ~[ ] >
}

< DEFAULT >
TOKEN : /* Reserved keywords and such */
{
  < CLASS : "class" >
| < BRACE_OPEN : "{" >
| < BRACE_CLOSE : "}" >
| < PARAN_OPEN : "(" >
| < PARAN_CLOSE : ")" >
| < ARRAY_DEF : "[]" >
| < THIS : "this" >
| < DOT : "." >
| < COMMA : "," >
| < SEMICOLON : ";" >
| < NULL : "null" >
| < NEW : "new" >
| < RETURN : "return" >
| < VAR : "var" >
| < IF : "if" >
| < ELSE : "else" >
| < WHILE : "while" >
| < ASSIGNMENT : ":=" >
| < MINUS : "-" >
| < NEGATION : "!" >
| < PLUS : "+" >
| < MULTIPLY : "*" >
| < DIVIDE : "/" >
| < REMAINDER : "%" >
| < EQUAL : "==" >
| < NOTEQUAL : "!=" >
| < LESS_THAN_EQUAL : "<=" >
| < GREATER_THAN_EQUAL : ">=" >
| < LESS_THAN : "<" >
| < GREATER_THAN : ">" >
| < AND : "&&" >
| < OR : "||" >
| < ID : ([ "a"-"z", "A"-"Z" ])+ >
| < INT : ([ "0"-"9" ]) { 1,8 } >
| < BOOL :
    (
      "true"
    | "false"
    ) >
| < STRING :
    "\""
    (
      " "
    | "!"
    | [ "#"-"~" ]
    )*
    "\"" >
| < BAD_TOKEN : ~[ ] >
}

ArrayList < ClassNode > file() :
{
  ArrayList < ClassNode > classes = new ArrayList < ClassNode > ();
  ClassNode cls;
}
{
  (
    cls = mini_class()
    {
      classes.add(cls);
    }
  )*
  < EOF >
  {
    return classes;
  }
}

ClassNode mini_class() :
{
  ClassNode cls = new ClassNode();
  MemberNode classMember;
}
{
  < CLASS >
  < ID > { cls.name = token; }
  < BRACE_OPEN >
  (
    classMember = classMember()
    {
      cls.children.add(classMember);
    }
  )*
  < BRACE_CLOSE >
  {
    return cls;
  }
}

MemberNode classMember() :
{
  FieldNode declaration = new FieldNode();
  MethodNode m;
  Token memberName;
  Type type;
}
{
  type = type() memberName = < ID >
  (
    < SEMICOLON >
    {
      declaration.name = memberName;
      declaration.type = type;
 
      return declaration;
    }
  | m = method()
  {
      m.name = memberName;
      m.returnType = type;
      return m;
    }
  )
}


MethodNode method() :
{
  MethodNode n = new MethodNode();
}
{
  n.arguments = signature() n.body = blockStatement()
  {
    return n;
  }
}

Type type() :
{
  Type t = new Type();
}
{
  t.baseType = < ID > (< ARRAY_DEF > { t.arrayDimensions++; })*
  {
    return t;
  }
}

ArrayList < NamedType > signature() :
{
  ArrayList < NamedType > arguments = new ArrayList < NamedType > ();
  NamedType nT = new NamedType();
}
{
  < PARAN_OPEN >
  (
    nT.type = type()
    nT.name=< ID >
	{
      arguments.add(nT);
    }
    (
      < COMMA >
      {
        nT = new NamedType();
      }
      nT.type = type()
      nT.name = < ID >
      {
        arguments.add(nT);
      }
    )*
 
  )?
  < PARAN_CLOSE >
  {
    return arguments;
  }
}

StatementNode statement() :
{
  StatementNode s = null;
}
{
  (
    s = blockStatement()
  | s = ifStatement()
  | s = whileStatement()
  | s = returnStatement()
  | s = simpleStatement()
  | s = declaration()
  )
  {
    return s;
  }
}

StatementNode simpleStatement() :
{
  ExpressionNode first, second;
}
{
  first = expression()
  (
    < SEMICOLON > { return new SimpleStatementNode(first); }
    | < ASSIGNMENT > second = expression() < SEMICOLON > {  return new AssignmentStatementNode(first, second); }
  )
}

DeclarationStatementNode declaration() :
{
  DeclarationStatementNode n = new DeclarationStatementNode();
}
{
  < VAR >
  < ID > { n.name = token; }
  < ASSIGNMENT >
  n.expression = expression()
  < SEMICOLON >
  {
    return n;
  }
}

BlockNode blockStatement() :
{
  BlockNode block = new BlockNode();
  StatementNode statement;
}
{
  < BRACE_OPEN >
  (
    statement=statement()
    {
      block.children.add(statement);
    }
  )*
  < BRACE_CLOSE >
  {
    return block;
  }
}

IfNode ifStatement() :
{
  IfNode n = new IfNode();
}
{
  < IF > < PARAN_OPEN > n.condition = expression()
  < PARAN_CLOSE > n.first = blockStatement()
  (
    < ELSE > n.second = blockStatement()
  )?
  {
    return n;
  }
}

WhileNode whileStatement() :
{
  WhileNode n = new WhileNode();
}
{
  < WHILE > < PARAN_OPEN > n.condition = expression()
  < PARAN_CLOSE > n.body = blockStatement()
  {
    return n;
  }
}

ReturnNode returnStatement() :
{
  ReturnNode n = new ReturnNode();
}
{
  < RETURN > n.value = expression() < SEMICOLON >
  {
    return n;
  }
}

ExpressionNode expression() :
{
  ExpressionNode n;
  UnaryExpressionNode unary;
}
{
  (
    unary = unaryOperator() unary.child = expression()
    {
      return unary;
    }
  | n = atomicExpression() (n = expression_suffix(n))?
  )
  {
    return n;
  }
}

ExpressionNode expression_suffix(ExpressionNode prefix) :
{
  BinaryExpressionNode b;
  MemberExpressionNode m = null;
}
{
    b=binaryOperator() b.second = expression()
	{
	  b.first = prefix;
	  b.balance();
	  return b;
	}
	| < DOT >
	  {
	    Token identifier;
	    ExpressionNode n=null;
	  }
	  identifier = < ID >
	  (m=argumentList())?
	  {
	    if(m == null){
	      m = new FieldMemberExpressionNode();
	    }
	    m.identifier = identifier;
	    m.child = prefix;
	  }
	  (n=expression_suffix(m))?
	  {
	    if(n != null)
	      return n;
	    return m;
	  }
}
UnaryExpressionNode unaryOperator() :
{}
{
  < MINUS > { return new MinusUnaryExpressionNode(token); }
| < NEGATION > { return new NegationUnaryExpressionNode(token); }
}

BinaryExpressionNode binaryOperator() :
{}
{
  < PLUS > { return new PlusBinaryExpressionNode(token); }
| < MINUS > { return new MinusBinaryExpressionNode(token); }
| < MULTIPLY > { return new MultiplyBinaryExpressionNode(token); }
| < DIVIDE > { return new DivideBinaryExpressionNode(token); }
| < REMAINDER > { return new RemainderBinaryExpressionNode(token); }
| < EQUAL > { return new EqBinaryExpressionNode(token); }
| < NOTEQUAL > { return new NeqBinaryExpressionNode(token); }
| < LESS_THAN_EQUAL > { return new LteBinaryExpressionNode(token); }
| < GREATER_THAN_EQUAL > { return new GteBinaryExpressionNode(token); }
| < LESS_THAN > { return new LtBinaryExpressionNode(token); }
| < GREATER_THAN > { return new GtBinaryExpressionNode(token); }
| < AND > { return new AndBinaryExpressionNode(token); }
| < OR > { return new OrBinaryExpressionNode(token); }
}

MethodMemberExpressionNode argumentList() :
{
  MethodMemberExpressionNode n = new MethodMemberExpressionNode();
  ExpressionNode expr;
}
{
  < PARAN_OPEN >
  (
    expr = expression()
    {
      n.children.add(expr);
    }
    (
      < COMMA > expr = expression()
      {
        n.children.add(expr);
      }
    )*
  )?
  < PARAN_CLOSE >
  {
    return n;
  }
}

ExpressionNode atomicExpression() :
{
  ExpressionNode n;
  MemberExpressionNode m = null;
  Token identifier;
}
{
  (
    identifier = < ID >
    ( m = argumentList())?
    {
      if(m == null){
	    m = new FieldMemberExpressionNode();
	  }
	  m.identifier = identifier;
	  return m;
    }
  | < THIS > { n = new ThisAtomicExpressionNode(token); }
  | < STRING > { n = new StringAtomicExpressionNode(token); }
  | < INT > { n = new IntAtomicExpressionNode(token); }
  | < BOOL > { n = new BoolAtomicExpressionNode(token); }
  | n = nullExpression()
  | n = newExpression()
  | < PARAN_OPEN > { PriorityExpressionNode priorityNode = new PriorityExpressionNode();}
    priorityNode.child = expression()
    < PARAN_CLOSE > { return priorityNode; }
  )
{
  return n;
}
}

NullExpressionNode nullExpression() :
{
  NullExpressionNode n = new NullExpressionNode();
}
{
  < NULL > < LESS_THAN > n.type = type() < GREATER_THAN >
  {
    return n;
  }
}

NewExpressionNode newExpression() :
{
  NewExpressionNode n = new NewExpressionNode();
}
{
  < NEW > < LESS_THAN > n.type = type()
  (
    < COMMA >
    (
      < INT >
    | < ID >
    )
    {
      n.arguments.add(token);
    }
  )*
  < GREATER_THAN >
  {
    return n;
  }
}
